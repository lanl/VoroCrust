# color dense mesh by voronoi shapes

read / avs / vor_tet.inp / motet
cmo / printatt / motet / -xyz- / minmax
cmo / printatt / motet / imt / minmax
cmo / printatt / motet / vor_vol / minmax
cmo / printatt / motet / tet_vol / minmax
cmo / set_id / motet / node / id_node
 
cmo / create / mohex / / / hex
 
#createpts / brick / xyz / 51 51 101 / -.5 -.5 -.5 / 1.5 1.5 2.5 / 1 1 1
createpts / brick / xyz / 201 201 301 / -.1 -.1 -.1 / 1.1 1.1 2.1 / 1 1 1
define CMO mohex

# element attributes to color voronoi volumes on to fine hex mesh
cmo/addatt/CMO/imt_tet/VINT/scalar/nelements/linear/permanent/agltn/0.0 
cmo/addatt/CMO/itp_tet/VINT/scalar/nelements/linear/permanent/agltn/0.0 
cmo/addatt/CMO/vor_tet/VDOUBLE/scalar/nelements/linear/permanent/agltn/0.0 
cmo setatt CMO imt 1
cmo setatt CMO itetclr 1
quality
 
# from tet voronoi volumes to hex elem attributes
# so we can extract internal surfaces in shape of voronoi volumes
interpolate/voronoi/ CMO itetclr /1 0 0/ motet id_node /keepatt
interpolate/voronoi/ CMO imt_tet /1 0 0/ motet imt /keepatt
interpolate/voronoi/ CMO itp_tet /1 0 0/ motet itp /keepatt
interpolate/voronoi/ CMO vor_tet /1 0 0/ motet vor_vol 
resetpts / itp
dump / avs / interp_hex.inp / CMO

# extract surfaces between vor volumes
extract/surfmesh/1,0,0/mos/ mohex
cmo setatt mos ipolydat no

cmo/copyatt/ mos mos/ itetclr itetclr0
cmo/select/mos
resetpts itp

# add attributes for viewing
define CMO mos
cmo/DELATT/CMO/ id_node
cmo/DELATT/CMO/ vor_vol
infile cmo_DELATT.mlgi

# element attributes to color voronoi volumes
cmo/addatt/CMO/imt_tet/VINT/scalar/nelements/linear/permanent/agltn/0.0
cmo/addatt/CMO/itp_tet/VINT/scalar/nelements/linear/permanent/agltn/0.0
cmo/addatt/CMO/vor_tet/VDOUBLE/scalar/nelements/linear/permanent/agltn/0.0
  interpolate/voronoi/ CMO imt_tet /1 0 0/ motet imt /keepatt
  interpolate/voronoi/ CMO itp_tet /1 0 0/ motet itp /keepatt
  interpolate/voronoi/ CMO vor_tet /1 0 0/ motet vor_vol


dump avs vc_vor_surfmesh.inp mos
dump gmv vc_vor_surfmesh.gmv mos

cmo/printatt/mos/-all- minmax

finish
# STOP HERE

cmo create motmp
copypts motmp mohex
dump / gmv / interp_hex_pts.gmv / motmp
pset/pdel/attribute imt/1,0,0/ ne 1
rmpoint pset,get,pdel
rmpoint compress
dump / gmv / interp_hex_imt1_pts.gmv / motmp

cmo select mohex
pset/pdel/attribute imt/1,0,0/ ne 1
rmpoint pset,get,pdel
rmpoint compress
resetpts itp
cmo setatt mohex ipolydat no
dump gmv interp_hex_imt1.gmv mohex


cmo printatt mohex -all- minmax

finish
