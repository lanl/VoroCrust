# connect points into tet mesh for fehm files
read avs surf_cube_1bndry.inp mo                                                

# write gmv file with attribute normals
cmo/copy/motmp/ mo
cmo/select/motmp
settets/normal
dump gmv surf_normals.gmv motmp ascii
cmo/delete/motmp

# check the tri surface mesh
quality                                                                         
cmo printatt mo itetclr minmax                                                  
cmo printatt mo imt minmax                                                      
filter 1,0,0                                                                    
resetpts itp                                                                    
boundary_components                                                             
quality                                                                         
cmo/addatt/mo/volume/tri_area                                                   
cmo printatt mo tri_area minmax                                                 
dump avs tri_node_attr.inp mo                                             
dump avs tri_node_attr.dat mo 0 0 1 0                                              

# connect into delaunay tet mesh
cmo/create/cmotet///tet                                                         
copypts cmotet mo                                                               
cmo delete mo

cmo select cmotet
connect                                                                         
cmo/setatt/cmotet/itetclr/1                                                     
resetpts/itp                                                                    
quality                                                                         
cmo/addatt/cmotet/volume/tet_vol                                                
cmo printatt cmotet tet_vol minmax                                              

# assign material 2 to top cube
pset/p2/attribute zic/1,0,0/ge 1.                                               
eltset/e2/inclusive pset,get,p2                                                 
eltset/e2/delete                                                                
eltset/e2/exclusive pset,get,p2                                                 
cmo setatt cmotet itetclr eltset,get,e2 2                                       
cmo setatt cmotet imt pset,get,p2 2                                             
resetpts itp                                                                    

# add some cell and node information
quality/volume/itetclr                                                          
cmo/addatt/cmotet/voronoi_volume/vor_vol                                        
cmo printatt cmotet vor_vol minmax                                              
resetpts itp                                                                    

# write attribute files
dump avs tet.inp cmotet                                                         
dump gmv tet.gmv cmotet                                                         
dump avs tet_node_attr.dat cmotet 0 0 1 0                                              
dump avs tet_cell_attr.dat cmotet 0 0 0 1                                              

# write fehm files
cmo/setatt/cmotet/itetclr/1                                                     
resetpts/itp

dump/fehm/tet/cmotet ascii                                          
dump/stor/tet_full/cmotet none/ ascii                                          

# report information
cmo printatt cmotet -all- minmax                                                

finish                                                                          
